/////////////////////////////////////////////////////////////////////////////
'功能描述：在好友列表启动脚本即可自动开始送心操作。
'支持屏幕宽高比约为9：16分辨率：480*854、540*960、720*1280、1080*1920及屏幕宽高比约为3：5分辨率：480*800、800*1280、1080*1800
'本作多点找色特征均在720*1280分辨率下获取进行缩放比例计算
'从零学起之安卓篇《如何在安卓模拟器中进行多分辨率开发调试》
'http://bbs.anjian.com/thread-537192-1-1.html
/////////////////////////////////////////////////////////////////////////////
'从零学起之安卓实战篇《天天酷跑好友送心》
'http://bbs.anjian.com/thread-536548-1-1.html
/////////////////////////////////////////////////////////////////////////////
//脚本作者：荆棘
//更新日期：2014年08月21日
/////////////////////////////////////////////////////////////////////////////
Randomize
Dim IntX,IntY
Dim win_x,win_y
win_x = GetScreenX()
win_y = GetScreenY()
TracePrint "当前分辨率：" & win_x & "*" & win_y
'设置浮窗移动到中间
SetControlBarPos 0.5
/////////////////////////////////////////////////////////////////////////////
Dim 好友列表(),红心图标(),送心成功(),到底图标(),下页图标(),划动距离(),标题范围(),返回图标()
/////////////////////////////////////////////////////////////////////////////
'比例计算
Dim win_w,win_h
win_w=win_x/720
win_h=win_y/1280
TracePrint "屏幕比例：" & win_w & ":" & win_h
If Abs(win_w-win_h) < 0.01 Then'经测试该游戏比例偏差小于0.01可以兼容
    TracePrint "720*1280比例"
    好友列表=比例换算(Array(65,160,"FFEBD6","0|30|CE6910,-30|70|BD6D10"))
    红心图标=比例换算(Array(110,760,510,850,"423DEF","-18|35|10C6FF,22|55|EFF7F7"))
    送心成功=比例换算(Array(170,330,"D69E00","0|420|00BEEF,100|60|FFF3E6"))
    到底图标=比例换算(Array(250,160,510,210,"735529","-15|-15|D6AA31,-2|98|FFEBCE,-62|-2|DEDBB5"))
    下页图标=比例换算(Array(130,440,550,620,"C58E5A","14|0|FFF7EE,-14|33|B5A294,-14|73|B5A294,0|125|B5A294"))
    划动距离=比例换算(Array(160,540,470,540))
    标题范围=比例换算(Array(560,220,570,320,"F7E29C",250))
    返回图标=比例换算(Array(599,140,"EFEFEF","0|20|EFEFEF,21|20|FFC611"))
End If
/////////////////////////////////////////////////////////////////////////////
If IsNumeric(好友列表(0))=False Then
    '比例计算
    win_w=win_x/480
    win_h=win_y/800
    'TracePrint win_w & "-" & win_h & "=" & Abs(win_w-win_h)
    If Abs(win_w-win_h) < 0.1 Then'经测试该游戏比例偏差小于0.1可以兼容
        TracePrint "480*800比例"
        好友列表=比例换算(Array(42,80,"FFE9CE","0|20|C7680E,0|45|E58F19"))
        红心图标=比例换算(Array(70,480,340,540,"423DEF","-12|25|19CAFF,13|40|EFF9FD"))
        送心成功=比例换算(Array(110,200,"D89A03","0|280|00BBF0,50|0|FFF4E8"))
        到底图标=比例换算(Array(160,75,340,115,"78552B","-11|-11|D8A82D,-4|66|FFE7C8,-19|136|C5821B,-73|194|F0EEE3"))
        下页图标=比例换算(Array(80,260,360,390,"C08C56","10|0|FFF7EF,-11|22|B8A69A,-9|49|B9A69A,-1|82|B8A79A"))
        划动距离=比例换算(Array(100,300,305,300))
        标题范围=比例换算(Array(370,115,380,195,"F7E29C",168))
        返回图标=比例换算(Array(392,65,"EEEEEE","0|15|EEEEEE,14|15|FFC717"))
    End If
End If
/////////////////////////////////////////////////////////////////////////////
If IsNumeric(好友列表(0))=False Then
    TracePrint "暂不支持当前" & win_x & "*" & win_y & "分辨率，请联系作者咨询！"
    ShowMessage "暂不支持当前" & win_x & "*" & win_y & "分辨率，请联系作者咨询！"
    Delay 1000
    EndScript
End If
/////////////////////////////////////////////////////////////////////////////
'调试区（不解释）
//KeepCapture
//TracePrint "好友列表"
//TracePrint 判断(好友列表)
//TracePrint IntX & "|" & IntY
//TracePrint "红心图标"
//TracePrint 判断(红心图标)
//TracePrint IntX & "|" & IntY
//TracePrint "送心成功"
//TracePrint 判断(送心成功)
//TracePrint IntX & "|" & IntY
//TracePrint "到底图标"
//TracePrint 判断(到底图标)
//TracePrint IntX & "|" & IntY
//TracePrint "下页图标"
//TracePrint 判断(下页图标)
//TracePrint IntX & "|" & IntY
//TracePrint "返回图标"
//TracePrint 判断(返回图标)
//TracePrint IntX & "|" & IntY
//ReleaseCapture
//Call 触屏划动(划动距离)
//TracePrint 切换排行榜(标题范围)
//EndScript
/////////////////////////////////////////////////////////////////////////////
'主线循环
Do
    KeepCapture
    If 判断(好友列表)=True Then
        TracePrint "好友列表"
        If 判断点击(红心图标)=True Then
            TracePrint "红心图标"
        ElseIf 判断点击(下页图标)=True Then
            TracePrint "下页图标"
        ElseIf 判断(到底图标)=True Then
            TracePrint "到底图标"
            If 切换排行榜(标题范围)=False Then
                TracePrint "已是最后排行榜"
                ShowMessage "已经全部刷新完毕！"
                Delay 1000
                EndScript //全部好友送心完成，脚本停止！
            End If
            //没结束，则继续循环
        Else
            //没红心图标，也没下一页图标，也没到底图标，那么就划动好友列表
            TracePrint "触屏划动"
            Call 触屏划动(划动距离)
        End If
    ElseIf 判断点击(送心成功)=True Then
        TracePrint "送心成功"
    ElseIf 判断点击(返回图标)=True Then
        TracePrint "返回图标"
    End If
    Delay 500
Loop
/////////////////////////////////////////////////////////////////////////////
'功能模块（跟进当前脚本需求，专门编写）
Function 切换排行榜(数组特征)
    切换排行榜=False
    Dim arr(),i
    arr=数组特征
    For i=0 To 1
        If Fc(arr(0),arr(1)+i*arr(5),arr(2),arr(3)+i*arr(5),arr(4))=True Then
            Tap arr(0),arr(1)+(i+1)*arr(5)
            Delay 500
            切换排行榜=True
            Exit For
        End If
    Next
End Function
/////////////////////////////////////////////////////////////////////////////
'固定模块（通用型封装模块，基本常用脚本都需要用到的功能）
Sub 触屏划动(数组特征)
    Dim arr(),n
    arr=数组特征
    n=Int(10*Rnd())'给划动操作增加些随机偏值
    TouchDown arr(0)+n,arr(1)+n,1
    TouchMove arr(2)+n,arr(3)+n,1
    Delay 500
    TouchUp 1
    Delay 100
End Sub
Function 判断(数组特征)
    判断=False
    Dim arr()
    arr=数组特征
    '因为本作中只用到区域多点找色查找与固定起点多点比色
    '所以就简单判断下第四个值是否为数字，即可确认是区域多点找色还是固定多点比色
    If IsNumeric(arr(3))=True Then
        判断=FindMultiCol(arr(0),arr(1),arr(2),arr(3),arr(4),arr(5),0.9)
    Else
        判断=CmpMultiCol(arr(0),arr(1),arr(2),arr(3),0.9)
    End If
End Function
Sub 点击(数组坐标)
    Dim arr()
    arr=数组坐标
    Tap arr(0),arr(1)
End Sub
Function 判断点击(数组特征)
    判断点击=False
    If 判断(数组特征)=True Then
        Tap IntX,IntY
        判断点击=True
    End If
End Function
Function 比例换算(数组)
    Dim arr(),arr_bl(),i,n
    arr=数组
    arr_bl=Array(win_w,win_h)
    i=0
    For Each n In arr
        If IsNumeric(n)=True Then
            arr(i)=n*arr_bl(i Mod 2)
        ElseIf InStr(1,n,"|")>0 Then
            Dim arr1(),str,arr2()
            arr1=Split(arr(i),",")
            str=""
            For Each n In arr1
                arr2=Split(n,"|")
                str=str & arr2(0)*win_w & "|" & arr2(1)*win_h & "|" & arr2(2) & ","
            Next
            arr(i)=Left(str,Len(str)-1)
            Exit For
        End If
        i=i+1
    Next
    比例换算=arr
End Function
/////////////////////////////////////////////////////////////////////////////
'本脚本相关教程资料（都是重点，想学就把我的教程看完，如不懂可以各大按键群（官方群）里联系我）
'从零学起之安卓篇《按键精灵安卓版找图找色应用汇总介绍》
'http://bbs.anjian.com/thread-523396-1-1.html
'从零学起之基础核心《麻雀虽小五脏俱全》
'http://bbs.anjian.com/thread-509172-1-1.html
/////////////////////////////////////////////////////////////////////////////
'固定函数（通用型封装模块，核心函数功能，每个脚本必用，一般没新需求是不更改的）
'判断两个16进制值相似度是否到达对应百分比
Function IsCol(col1,col2,n)
    IsCol=False
    Dim r1,g1,b1
    Dim r2,g2,b2
    ColorToRGB col1,r1,g1,b1
    ColorToRGB col2,r2,g2,b2
    If 1 - (Abs(r1 - r2) + Abs(g1 - g2) + Abs(b1 - b2)) / 255 / 3 >= n Then IsCol=True
End Function
'固定坐标单点模糊找色
Function CmpCol(x,y,col,n)
    CmpCol=False
    If x < 0 or y < 0 Then Exit Function
    Dim temp_col
    temp_col=GetPixelColor(x,y)
    If temp_col=col Then
        CmpCol=True
    Else
        CmpCol=IsCol(col,temp_col,n)
    End If
End Function
'固定坐标多点模糊找色
Function CmpMultiCol(x,y,col,str,n)
    CmpMultiCol=False
    IntX=x
    IntY=y
    If CmpCol(x,y,col,n)=True Then
        '第一个点颜色符合，调用MultiColor多点处理模块匹配其余点的颜色是否正确
        If MultiColor(x,y,str,n)=True Then
            CmpMultiCol=True
            Exit Function
        End If
    End If
    '第一个点颜色不符，调用FindMultiColor多点找色模块，对该点再次进行一次小范围多点找色查找
    CmpMultiCol=FindMultiCol(x-1,y-20,x+1,y+20,col,str,n)
End Function
'区域范围多点模糊找色
Function FindMultiCol(x1,y1,x2,y2,col,str,n)
    FindMultiCol=False
    Dim x,y
    Do
        FindColor x1,y1,x2,y2,col,0,n,intX, intY
        If intX >= 0 Then
            If MultiColor(intX,intY,str,n)=True Then
                FindMultiCol=True
                Exit Do
            Else
                x=IntX
                y=IntY
                Do Until x > x2
                    FindColor x,y,x2,y,col,0,n,intX,intY
                    If intX >= 0 Then
                        If MultiColor(intX,intY,str,n)=True Then
                            FindMultiCol=True
                            Exit Function
                        Else
                            x=IntX+1
                        End If
                    Else
                        Exit Do
                    End If
                Loop
                y1=y+1
            End If
        Else
            Exit Do
        End If
    Loop Until y1 > y2
End Function
'多点偏移点阵匹配
Function MultiColor(x,y,str,n)
    MultiColor=False
    Dim arr1,i,arr2()
    arr1=Split(str,",")
    For Each i In arr1
        arr2=Split(i,"|")
        If CmpCol(x+Int(arr2(0)),y+Int(arr2(1)),arr2(2),n)=False Then
            'FindColor x+Int(arr2(0))-3, y+Int(arr2(1))-3, x+Int(arr2(0))+3, y+Int(arr2(1))+3, arr2(2), 0, n, intX, intY
            'If IntX=-1 Then 
            Exit Function
        End If
    Next
    MultiColor=True
End Function
//////////////////////////////////////////////////////////////////////////////////////////
'自定义固定模块组合应用，以下部分大家请理解函数模块功能作用后自行组合使用。（请理解后调用，以免出错都不懂原因）
Function Cm(x,y,col,str)
    Cm=CmEx(x,y,col,str,0.9)
End Function
Function CmEx(x,y,col,str,n)
    KeepCapture
    CmEx=CmpMultiCol(x,y,col,str,n)
    ReleaseCapture
End Function
Function Fm(x1,y1,x2,y2,col,str)
    Fm=FmEx(x1,y1,x2,y2,col,str,0.9)
End Function
Function FmEx(x1,y1,x2,y2,col,str,n)
    KeepCapture
    FmEx=FindMultiCol(x1,y1,x2,y2,col,str,n)
    ReleaseCapture
End Function
Function Fc(x1,y1,x2,y2,col)
    Fc=FcEx(x1,y1,x2,y2,col,0.9)
End Function
Function FcEx(x1,y1,x2,y2,col,n)
    FcEx=False
    FindColor x1,y1,x2,y2,col,0,n,intX,intY
    If intX >= 0 Then FcEx=True
End Function
//////////////////////////////////////////////////////////////////////////////////////////
'作业
//1、Function 比例换算(数组)，原理描述完整写出来。
//2、Function 判断(数组特征) 与 Sub 点击(数组坐标)，各列出至少一个优点描述。
//3、Sub 点击技能()，为什么里面代码这么写，列出至少两点。
//4、Function CmpMultiCol(x,y,col,str,n)，固定多点找色原理描述。
//5、Function FindMultiCol(x1,y1,x2,y2,col,str,n)，区域多点找色原理描述。
//6、Cm\CmEx\Fm\FmEx\Fc，这几个函数模块用意描述出来。
//////////////////////////////////////////////////////////////////////////////////////////

//脚本作者：荆棘
//更新日期：2014年08月21日